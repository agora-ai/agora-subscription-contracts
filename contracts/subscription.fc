#include "imports/stdlib.fc";
#include "imports/op_codes/subscription.fc";
#include "imports/op_codes/subscription_master.fc";
#include "imports/op_codes/walletv4.fc";
#include "imports/errors/subscription.fc";
#include "imports/utils.fc";


;; Title: Agora Service Subscription
;; Author: Yuri Fernandes (0xyuri.ton)
;; Description: Subtract subscription fees from owner wallet

;; Storage variables

;; subscription_master: Address of the subscription master associated with this
;;  subscription
;; owner: Subscription owner wallet address
;; manager: Receive fees and payments from this subscription
;; fee: Fee to be substract by the user periodically
;; period: Fee payment interval (in seconds)
;; activation_fee: Fee paid by the user on activation
;; last_paid: Unix timestamp of the last payment
;; activated?: Whether the subscription is activated or not
global slice storage::subscription_master;
global slice storage::owner;
global slice storage::manager;
global int storage::fee;
global int storage::period;
global int storage::activation_fee;
global int storage::last_paid;
global int storage::activated?;

;; Constants
const int MIN_TON_RESERVE = 50000000;

;; Auxiliary functions
int payment_due?() inline {
    return now() - storage::last_paid >= storage::period;
}

cell assemble_request_funds_msg(int query_id, int toncoins, cell extra) inline {
    return begin_cell()
        .store_uint(0x18, 6)
        .store_slice(storage::owner)
        .store_coins(0)
        .store_uint(0, 1 + 4 + 4 + 64 + 32 + 1 + 1)
        .store_uint(op::request_funds, 32)
        .store_uint(query_id, 64)
        .store_coins(toncoins)
    .end_cell();
}

cell assemble_collect_fee_msg(int query_id) inline {
    return begin_cell()
        .store_uint(0x10, 6)
        .store_slice(storage::manager)
        .store_coins(storage::activation_fee)
        .store_uint(0, 1 + 4 + 4 + 64 + 32 + 1 + 1)
        .store_uint(op::activation_fee_transfer, 32)
        .store_uint(query_id, 64)
        .store_slice(storage::owner)
    .end_cell();
}

cell assemble_subscription_created_log(int query_id) inline {
    return begin_cell()
        .store_uint(0x10, 6)
        .store_slice(storage::subscription_master)
        .store_coins(0)
        .store_uint(0, 1 + 4 + 4 + 64 + 32 + 1 + 1)
        .store_uint(op::log_subscription_created, 32)
        .store_uint(query_id, 64)
        .store_slice(storage::owner)
    .end_cell();
}

;; Internal Messages
() collect_activation_fee(int query_id, int remaining_value) impure inline {
    throw_if(error::insufficient_funds, remaining_value < storage::activation_fee + MIN_TON_RESERVE);
    send_raw_message(assemble_collect_fee_msg(query_id), 1);
}

() send_subscription_created_log(int query_id) impure inline {
    send_raw_message(assemble_subscription_created_log(query_id), 128);
}

;; Storage (C4) manipulation functions
() load_init_data() impure {
    var ds = get_data().begin_parse();
    storage::subscription_master = ds~load_msg_addr();
    storage::owner = ds~load_msg_addr();
    throw_unless(error::init_true, ds.slice_empty?());
}

() load_data() impure {
    var ds = get_data().begin_parse();
    storage::subscription_master = ds~load_msg_addr();
    storage::owner = ds~load_msg_addr();
    throw_if(error::init_false, ds.slice_empty?());
    storage::manager = ds~load_msg_addr();
    storage::activation_fee = ds~load_coins();
    storage::fee = ds~load_coins();
    storage::period = ds~load_uint(32);
    storage::last_paid = ds~load_uint(32);
    storage::activated? = ds~load_int(1);
}

() save_data() impure {
    set_data(begin_cell()
        .store_slice(storage::subscription_master)
        .store_slice(storage::owner)
        .store_slice(storage::manager)
        .store_coins(storage::activation_fee)
        .store_coins(storage::fee)
        .store_uint(storage::period, 32)
        .store_uint(storage::last_paid, 32)
        .store_int(storage::activated?, 1)
    .end_cell());
}

;; OP functions
() init(slice sender_addr, int query_id, slice msg_body_content) impure inline {
    load_init_data();
    throw_unless(error::not_admin, sender_addr.equal_slice_bits(storage::subscription_master));

    msg_body_content~load_msg_addr();
    storage::manager = msg_body_content~load_msg_addr();
    storage::activation_fee = msg_body_content~load_coins();
    storage::fee = msg_body_content~load_coins();
    storage::period = msg_body_content~load_uint(32);
    storage::last_paid = now();
    storage::activated? = false;

    raw_reserve(MIN_TON_RESERVE, 0);

    send_subscription_created_log(query_id);

    save_data();
}

() activate(slice sender_addr, int query_id, int remaining_value, int save_balance) impure inline {
    throw_unless(error::not_owner, sender_addr.equal_slice_bits(storage::owner));
    throw_if(error::activated, storage::activated?);

    ;; collect activation fee from new user
    collect_activation_fee(query_id, remaining_value);

    storage::activated? = true;

    reply(query_id, sender_addr, save_balance, 128);

    save_data();
}

() deactivate(slice sender_addr, int query_id) impure inline {
    throw_unless(error::not_owner, sender_addr.equal_slice_bits(storage::owner));
    throw_unless(error::not_activated, storage::activated?);

    storage::activated? = false;

    reply(query_id, sender_addr, 0, 64);

    save_data();
}

() update_authority(slice sender_addr, int query_id, slice msg_body_content) impure inline {
    throw_unless(error::not_master, sender_addr.equal_slice_bits(storage::subscription_master));

    slice new_manager = msg_body_content~load_msg_addr();
    throw_if(error::no_storage_change, new_manager.equal_slice_bits(storage::manager));

    storage::manager = new_manager;

    reply(query_id, sender_addr, 0, 64);

    save_data();
}

() request_payment_internal(slice sender_addr, int query_id, slice msg_body_content) impure inline {
    throw_unless(error::not_admin, sender_addr.equal_slice_bits(storage::manager));
    throw_unless(error::not_activated, storage::activated?);
    throw_unless(error::fee_period, payment_due?());

    ;; adjust `last_paid` to the beginning of the period interval
    storage::last_paid = (now() - storage::last_paid) / storage::period * storage::period + storage::last_paid;

    ;; Ignore the case that more than one period has passed, it's responsibility of the
    ;; service to execute payment requests in time.
    ;; Allow only Toncoin payments (pass empty extra dict)
    send_raw_message(assemble_request_funds_msg(query_id, storage::fee, new_dict()), 64);

    save_data();
}

() request_payment_external(int query_id, slice msg_body_content) impure inline {
    throw_unless(error::not_activated, storage::activated?);
    throw_unless(error::fee_period, payment_due?());
    accept_message();

    ;; adjust `last_paid` to the beginning of the period interval
    storage::last_paid = (now() - storage::last_paid) / storage::period * storage::period + storage::last_paid;

    ;; Ignore the case that more than one period has passed, it's responsibility of the
    ;; service to execute payment requests in time.
    ;; Allow only Toncoin payments (pass empty extra dict)
    send_raw_message(assemble_request_funds_msg(query_id, storage::fee, new_dict()), 1);

    save_data();
}

() bounce_request_funds() impure inline {
    load_data(); ;; load storage variables

    storage::activated? = false;

    save_data();
}

() recv_external(slice in_msg_body) impure {
    int op = in_msg_body~load_uint(32); ;; by convention, the first 32 bits of incoming message is the op
    int query_id = in_msg_body~load_uint(64); ;; also by convention, the next 64 bits contain the "query id", although this is not always the case

    load_data(); ;; load storage variables

    if (op == op::request_payment) {
        request_payment_external(query_id, in_msg_body);
        return ();
    }
}

() recv_internal(int my_balance, int msg_value, cell in_msg_full, slice in_msg_body) impure {
    if (in_msg_body.slice_empty?()) { ;; receive ton transfers (simple transfer without message)
        return ();
    }

    int op = in_msg_body~load_uint(32); ;; by convention, the first 32 bits of incoming message is the op
    int query_id = in_msg_body~load_uint(64); ;; also by convention, the next 64 bits contain the "query id", although this is not always the case

    slice cs = in_msg_full.begin_parse();
    int flags = cs~load_uint(4);
    slice sender_addr = cs~load_msg_addr();
    if (flags & 1) {
        if (op == op::request_funds) {
            bounce_request_funds();
        }
        return ();
    }

    ;; perform op::init before `load_data`, since it still doesn't have the
    ;; full storage, so `load_data` would fail.
    if (op == op::init) {
        init(sender_addr, query_id, in_msg_body);
        return ();
    }

    load_data(); ;; load storage variables

    if (op == op::activate_subscription) {
        activate(sender_addr, query_id, msg_value, msg_value);
        return ();
    }

    if (op == op::deactivate_subscription) {
        deactivate(sender_addr, query_id);
        return ();
    }

    if (op == op::update_authority) {
        update_authority(sender_addr, query_id, in_msg_body);
        return ();
    }

    if (op == op::request_payment) {
        request_payment_internal(sender_addr, query_id, in_msg_body);
        return ();
    }

    throw(error::unknown_op);
}

(slice, slice, slice, int, int, int, int, int) get_subscription_data() method_id {
    load_data();
    return (
        storage::subscription_master,
        storage::owner,
        storage::manager,
        storage::activation_fee,
        storage::fee,
        storage::period,
        storage::last_paid,
        storage::activated?
    );
}

int is_init() method_id {
    var ds = get_data().begin_parse();
    ds~load_msg_addr();
    ds~load_msg_addr();
    return ~ ds.slice_empty?();
}

slice get_subscription_master() method_id {
    load_data();
    return storage::subscription_master;
}

slice get_subscriber() method_id {
    load_data();
    return storage::owner;
}

slice get_manager() method_id {
    load_data();
    return storage::manager;
}

(int, int, int, int) get_fee_info() method_id {
    load_data();
    return (
        storage::activation_fee,
        storage::fee,
        storage::last_paid,
        storage::period
    );
}

int is_payment_due() method_id {
    load_data();
    return payment_due?();
}

int is_activated() method_id {
    load_data();
    return storage::activated?;
}

int is_fulfilled() method_id {
    load_data();
    return storage::activated? & ~ payment_due?();
}